module Twigen exposing (main)

import Base64
import Browser exposing (Document, UrlRequest)
import Browser.Navigation as Nav
import Bytes exposing (Bytes)
import Bytes.Encode as BE
import Bytes.Decode as BD
import Html exposing (Html, div, h1, h3, button, ul, li, text, textarea, br, table, tbody, tr, td, select, option, input, p, a)
import Html.Attributes as Attr
import Html.Events exposing (..)
import Random exposing (Generator, map, map2, map3, lazy)
import Task
import Url exposing (Url)



main =
    Browser.application
        { init = init
        , view = view
        , update = update
        , subscriptions = subscriptions
        , onUrlRequest = LinkClicked
        , onUrlChange = UrlChanged
        }



-- MODEL


type alias Model =
    { key : Nav.Key
    , url : Url
    , sentences : List String
    , tango : TangoData
    , tuikaSettei : TuikaSettei
    }

type alias TangoData =
    { meisi : List Meisi
    , keiyousi : List Keiyousi
    , dousi : List Dousi
    }

type alias TuikaSettei =
    { gokan : String
    , gyou : String
    , katuyoukei : String
    , syurui : String
    }



-- UPDATE


type Msg
    = Roll
    | NewSentences (List String)
    | TangoUpdate TangoData
    | TuikaSetteiUpdate TuikaSettei
    | LinkClicked UrlRequest
    | UrlChanged Url

update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        Roll ->
            let
                command =
                    Random.generate NewSentences
                        <| Random.list 10
                        <| sentence model.tango
            in
                ( model, command )

        NewSentences newSentences ->
            ( { model | sentences = newSentences }, Cmd.none )

        TangoUpdate tango ->
            ( { model | tango = tango }
            , Nav.pushUrl model.key <| Url.toString <| updateQuery tango model.url
            )

        TuikaSetteiUpdate settei ->
            ( { model | tuikaSettei = settei }, Cmd.none )

        LinkClicked urlRequest ->
            case urlRequest of
                Browser.Internal url ->
                    ( model
                    , Nav.pushUrl model.key (Url.toString url)
                    )

                Browser.External href ->
                    ( model
                    , Nav.load href
                    )

        UrlChanged url ->
            let
                model_ =
                    { model | url = url }

                maybeTango =
                    url.query
                        |> Maybe.andThen tangoFromQuery
            in
                case maybeTango of
                    Nothing ->
                        ( model_, Cmd.none )

                    Just tango ->
                        ( { model_ | tango = tango }, Cmd.none )

updateQuery : TangoData -> Url -> Url
updateQuery tango url =
    { url | query = tangoToQuery tango }



-- VIEW


view : Model -> Document Msg
view model =
    { title = "„ÇØ„ÇΩ„ÉÑ„Ç§„Ç∏„Çß„Éç„É¨„Éº„Çø"
    , body =
        [ mainView model
        , tweetButton model.url
        , p []
            [ text <| "code:"
            , a
                [ Attr.href "https://github.com/YuyaAizawa/Twigen"]
                [ text <| "github.com/YuyaAizawa/Twigen" ]
            ]
        ]
    }

mainView : Model -> Html Msg
mainView model =
    div []
        [ h1 [][ text "„ÇØ„ÇΩ„ÉÑ„Ç§„Ç∏„Çß„Éç„É¨„Éº„Çø" ]
        , ul []
          (model.sentences
              |> List.map (\s ->
                  li[][text s]
              )
          )
        , button [ onClick Roll ] [ text "Roll" ]
        , div [ Attr.style "display" "flex" ]
            [ div []
                [ h3 [][ text <| "ÂêçË©û" ]
                , textarea
                    [ Attr.value <| (model.tango.meisi |> String.join "\n")
                    , onInput <| meisiKousin model.tango
                    , Attr.style "resize" "none"
                    ][]
                ]
            , div []
                [ h3 [][ text <| "ÂΩ¢ÂÆπË©û" ]
                , textarea
                    [ Attr.value <| (model.tango.keiyousi |> String.join "\n")
                    , onInput <| keiyousiKousin model.tango
                    , Attr.style "resize" "none"
                    ][]
                ]
            ]
        , div []
            [ h3 [][ text <| "ÂãïË©û" ]
            , table []
                [ tbody
                    [ Attr.style "overflow-y" "scroll"
                    , Attr.style "display" "block"
                    , Attr.style "height" "100px"
                    ]
                    ( model.tango.dousi
                        |> List.indexedMap (\i (Dousi gokan katuyou syurui) ->
                            tr []
                                [ td [][ text <| gokan ]
                                , td [][ text <| katuyouToString katuyou ]
                                , td [][ text <| dousiSyuruiToString <| syurui ]
                                , td []
                                    [ button
                                        [ onClick <| dousiSakujo i model.tango ]
                                        [ text <| "ÂâäÈô§" ]
                                    ]
                                ]
                        )
                    )
                ]
            , input
                [ Attr.type_ "text"
                , Attr.placeholder "ÂãïË©û„ÅÆË™ûÂππ"
                , onInput <| gokanSettei model.tuikaSettei
                ][]
            , select
                [ onInput <| gyouSettei model.tuikaSettei ]
                [ option [ Attr.value "" ][ text <| "(„Å™„Åó)" ]
                , option [ Attr.value "„ÅÇ" ][ text <| "„ÅÇË°å" ]
                , option [ Attr.value "„Åã" ][ text <| "„ÅãË°å" ]
                , option [ Attr.value "„Åå" ][ text <| "„ÅåË°å" ]
                , option [ Attr.value "„Åï" ][ text <| "„ÅïË°å" ]
                , option [ Attr.value "„Åñ" ][ text <| "„ÅñË°å" ]
                , option [ Attr.value "„Åü" ][ text <| "„ÅüË°å" ]
                , option [ Attr.value "„Å†" ][ text <| "„Å†Ë°å" ]
                , option [ Attr.value "„Å™" ][ text <| "„Å™Ë°å" ]
                , option [ Attr.value "„Å∞" ][ text <| "„Å∞Ë°å" ]
                , option [ Attr.value "„Åæ" ][ text <| "„ÅæË°å" ]
                , option [ Attr.value "„Çâ" ][ text <| "„ÇâË°å" ]
                , option [ Attr.value "„Çè" ][ text <| "„ÇèË°å" ]
                ]
            , select
                [ onInput <| katuyoukeiSettei model.tuikaSettei ]
                [ option [ Attr.value "‰∫îÊÆµ" ][ text <| "‰∫îÊÆµ" ]
                , option [ Attr.value "‰∏ä‰∏Ä" ][ text <| "‰∏ä‰∏Ä" ]
                , option [ Attr.value "‰∏ã‰∏Ä" ][ text <| "‰∏ã‰∏Ä" ]
                , option [ Attr.value "„ÅïÂ§âÊ†º" ][ text <| "Â§âÊ†º" ]
                ]
            , select
                [ onInput <| syuruiSettei model.tuikaSettei ]
                [ option [ Attr.value "Ëá™ÂãïË©û" ][ text <| "Ëá™ÂãïË©û" ]
                , option [ Attr.value "‰ªñÂãïË©û" ][ text <| "‰ªñÂãïË©û" ]
                , option [ Attr.value "‰∏°Êñπ"   ][ text <| "‰∏°Êñπ" ]
                ]
            , button
                [ onClick <| dousiTuika model.tuikaSettei model.tango ]
                [ text <| "ËøΩÂä†" ]
            ]
        ]

meisiKousin : TangoData -> String -> Msg
meisiKousin tango str =
    let
        meisi_ =
            str
                |> String.split "\n"
    in
        TangoUpdate { tango | meisi = meisi_ }

keiyousiKousin : TangoData -> String -> Msg
keiyousiKousin tango str =
    let
        keiyousi_ =
            str
                |> String.split "\n"
    in
        TangoUpdate { tango | keiyousi = keiyousi_ }

gokanSettei : TuikaSettei -> String -> Msg
gokanSettei settei gokan =
    TuikaSetteiUpdate { settei | gokan = gokan }

gyouSettei settei gyou =
    TuikaSetteiUpdate { settei | gyou = gyou }

katuyoukeiSettei settei katuyoukei =
    TuikaSetteiUpdate { settei | katuyoukei = katuyoukei }

syuruiSettei settei syurui =
    TuikaSetteiUpdate { settei | syurui = syurui }

dousiTuika settei tango =
    let
        dousi_ =
            Maybe.map2
            (\k s -> Dousi settei.gokan k s)
            (katuyouFromString settei.gyou settei.katuyoukei)
            (dousiSyuruiFromString settei.syurui)

        list_ =
            case dousi_ of
                Just d -> d :: tango.dousi
                Nothing -> tango.dousi
    in
        TangoUpdate { tango | dousi = list_ }

dousiSakujo at tango =
    let
        help at_ rest result =
            case ( at_, rest ) of
                ( 0, hd :: tl ) ->
                    help (-1) tl result

                ( n, hd :: tl ) ->
                    help (n - 1) tl (hd :: result)

                _ ->
                    result |> List.reverse
    in
        TangoUpdate { tango | dousi = help at tango.dousi [] }


tweetButton : Url -> Html Msg
tweetButton url =
    a
        [ Attr.href "https://twitter.com/share"
        , Attr.class "twitter-share-button"
        , Attr.attribute "data-text" "Â•Ω„Åç„Å™ÂçòË™û„Åß„Åè„Åô„Å£„Å®„Åß„Åç„Çã„ÇØ„ÇΩ„ÉÑ„Ç§„ÇíÁîüÊàêÔºÜ„Ç∑„Çß„Ç¢"
        , Attr.attribute "data-url" (Url.toString url)
        , Attr.attribute "data-hashtags" "„ÇØ„ÇΩ„ÉÑ„Ç§„Ç∏„Çß„Éç„É¨„Éº„Çø"
        , Attr.attribute "data-related" "TypedTypelessTy"
        ]
        [ text <| "‰ΩúÊàê„Åó„Åü„Ç∏„Çß„Éç„É¨„Éº„Çø„ÇíTweet„ÅßÂÖ±Êúâ" ]

-- SUBSCRIPTIONS


subscriptions : Model -> Sub Msg
subscriptions model =
    Sub.none



-- TWEET PATTERN DIFINITIONS


sentence tango =
    choice
    [ intermittentReference tango
    , dagaomaeha tango
    , declarative tango
    , declarative tango -- weight
    , kimidakeno tango
    , haityuu tango
    , otherSentence tango
    ]

intermittentReference tango =
    choice
    [ seq [ meisiKu tango, c "Ôºå", dousiKu tango |> map dousiKuRenyou1, c "„Åå„Å°Ôºé" ]
    , seq [ meisiKu tango, c "Ôºå", meisiKu tango, c "„Åø„Åå„ÅÇ„ÇãÔºé" ]
    , seq [ meisiKu tango, c "Ôºå", meisi tango, c "„Åò„ÇÉ„ÇìÔºé" ]
    ]

dagaomaeha tango =
    let
        ore =
            choice
            [ c "‰ø∫"
            , c "„ÉØ„Ç§"
            , c "ÁßÅ"
            ]

        body =
            choice
            [ meisiKu tango |> map (\m -> m++"„Å†")
            , keiyousiGokan tango |> map keiyousiSyuusi
            , dousiKu tango |> map dousiKuSyuusi
            ]

        omae =
            choice
            [ c "„ÅäÂâç"
            , c "„Åç„Åø"
            , c "„ÅÇ„Å™„Åü"
            ]
    in
        seq [ ore, c "„ÅØ", body, c "„Åå", omae, c "„ÅØÔºü" ]

otherSentence tango =
    choice
    [ seq [ meisiKu tango, c "„ÅßÁî≥„ÅóË®≥„Å™„ÅÑ„Çàüò¢" ]
    , seq [ meisiKu tango, c "„ÅÆNASA" ]
    , seq [ keiyousi tango |> map keiyousiSyuusi, c "„ÅÑ„ÅÑ„ÅÑ„ÅÑ„ÅÑ„ÅÑ„ÅÑ„ÅÑ‚úå('œâ'‚úå )‰∏â‚úå('œâ')‚úå‰∏â( ‚úå'œâ')‚úå" ]
    , seq [ c "ÁÑ°Èôê„Å´", tadousi tango |> map dousiMizen, c "„Çå„Çã„ÅäÂâç„ÅÆ‰∫∫Áîü" ]
    , seq [ c "(", meisiKu tango, c "„ÅØ", tadousi tango |> map dousiMizen, c ")„Å™„ÅÑ„Åß„Åô" ]
    , seq [ jidousi tango |> map dousiMeirei, c "ÔºÅ„Åù„Å™„Åü„ÅØ", keiyousi tango |> map keiyousiSyuusi ]
    ]

declarative tango =
    let
        body =
            choice
            [ seq [ meisiKu tango, c "„ÅØ", meisiKu tango, choice [ c "", c "„Å†", c "„Åß„ÅÇ„Çã", c "„Åß„Å™„ÅÑ", c "„Åß„Åß„Åç„Å¶„ÅÑ„Çã" ] ]
            , seq [ meisiKu tango, c "„Å´„ÅØ", meisiKu tango, c "„Åå„ÅÇ„Çã" ]
            , seq [ meisiKu tango, c "„ÅØ", keiyousiGokan tango |> map keiyousiSyuusi ]
            , seq [ meisiKu tango, c "„ÅØ", dousiKu tango |> map dousiKuSyuusi, choice [ c "", c "„Åì„Å®„Åå„ÅÇ„Çã"] ]
            , seq [ meisiKu tango, c "„ÅØ", dousiKu tango |> map dousiKuMizen, c "„Å™„ÅÑ", choice [ c "", c "„Åì„Å®„Åå„ÅÇ„Çã"] ]
            , seq [ meisiKu tango, c "„Çí", tadousi tango |> map dousiRenyou2, c "„Å¶„ÅØ„ÅÑ„Åë„Å™„ÅÑ" ]
            ]
    in
        choice
        [ seq [ body, c "Ôºé #ÂÆ£Ë®ÄÁöÑÁü•Ë≠ò" ]
        , seq [ body, c "Ôºà„Éõ„É≥„Éû„ÅãÔºü"]
        , seq [ body, c "ÔºéÁü•„Çâ„Çì„Åë„Å©Ôºé"]
        , seq [ body, c "„Å£„Å¶È≠îÂâ§Ôºü" ]
        , seq [ c "„ÇÇ„Åó„Åã„Åó„Å¶Ôºö", body ]
        ]

kimidakeno tango =
    seq
    [ c "Âêõ„Å†„Åë„ÅÆ"
    , meisi tango
    , c "„Çí"
    , tadousi tango |> map dousiRenyou2
    , c "„Å¶ÊúÄÂº∑„ÅÆ"
    , meisi tango
    , c "„Çí‰Ωú„ÇäÂá∫„ÅõÔºÅ"
    ]

haityuu tango =
    Random.map2
    (\s1 ( s2, d ) ->
        s1 ++ "„ÅØ" ++ s2 ++ dousiSyuusi d ++ "Â†¥Âêà„Å®" ++ dousiMizen d ++ "„Å™„ÅÑÂ†¥Âêà„Åå„ÅÇ„Çã„Åû„ÅÑÔºé"
    )
    (meisiKu tango)
    (dousiKu tango)


choice : List (Generator a) -> Generator a
choice list =
    case list of
        [] -> Random.constant <| Debug.todo "Error"
        hd :: tl -> Random.uniform hd tl |> Random.andThen (\r -> r)

seq : List (Generator String) -> Generator String
seq list =
    case list of
    [] -> Random.constant ""
    hd :: tl -> Random.map2 (\hdStr tlStr -> hdStr ++ tlStr) hd (seq tl)

c = Random.constant



--- SYNTAX


type alias Meisi = String

meisi : TangoData -> Generator String
meisi tango =
    let
        sahenMeisi =
            tango.dousi
                |> List.filter (\(Dousi _ katuyoukei _) -> katuyoukei == Sahen )
                |> List.map (\(Dousi gokan _ _) -> gokan)
    in
        tango.meisi ++ sahenMeisi
            |> generatorFromList "‚óã‚óã"

meisiKu : TangoData -> Generator String
meisiKu tango =
    choice
    [ meisi tango
    , meisi tango -- weight
    , meisi tango -- weight
    , map2 -- „Äå„ÅÆ„Äç
        (\m1 m2 -> m1 ++ "„ÅÆ" ++ m2)
        (lazy (\_ -> meisiKu tango))
        (meisi tango)
    , map2 -- ÂΩ¢ÂÆπË©û„ÅÆÈÄ£‰ΩìÂΩ¢
        (\k m -> keiyousiRentai k ++ m)
        (keiyousi tango)
        (meisi tango)
    , map2 -- Áî®Ë®Ä„ÅÆÈÄ£‰ΩìÂΩ¢
        (\dk m -> dousiKuRentai dk ++ m)
        (lazy (\_ -> dousiKu tango))
        (meisi tango)
    ]


type alias Keiyousi = String

keiyousi : TangoData -> Generator KeiyousiGokan
keiyousi tango =
    tango.keiyousi
        |> generatorFromList dummyKeiyousi
        |> map KeiyousiGokan

type KeiyousiGokan = KeiyousiGokan String

keiyousiGokan : TangoData -> Generator KeiyousiGokan
keiyousiGokan tango =
    let
        ppoi =
            generatorFromList ""
            [ "„Å£„ÅΩ"
            , "„Çâ„Åó"
            ]

        nikui =
            generatorFromList ""
            [ "Èõ£"
            , "Ëæõ"
            , "„ÇÑ„Åô"
            ]
    in
        choice
        [ keiyousi tango
        , keiyousi tango -- weight
        , keiyousi tango -- weight
        , keiyousi tango -- weight
        , map3 -- ÂêçË©û Âä©ÂãïË©û
            (\m j (KeiyousiGokan k) -> m ++ j ++ "„Åè" ++ k |> KeiyousiGokan)
            (meisiKu tango)
            ppoi
            (keiyousi tango)
        , map3 -- ÂãïË©û„ÅÆÈÄ£Áî®ÂΩ¢Ôºã‰ªòÂ±ûË™û
            (\dk n (KeiyousiGokan k) -> dousiKuRenyou1 dk ++ n ++ "„Åè" ++ k |> KeiyousiGokan)
            (dousiKu tango)
            nikui
            (keiyousi tango)
        ]

keiyousiKatuyou : Katuyoukei -> KeiyousiGokan -> String
keiyousiKatuyou katuyoukei (KeiyousiGokan gokan) =
    let
        gobi =
            case katuyoukei of
                Mizen   -> "„Åã„Çç"
                Renyou1 -> "„Åã„Å£"
                Renyou2 -> "„Åè"
                Syuusi  -> "„ÅÑ"
                Rentai  -> "„ÅÑ"
                Katei   -> "„Åë„Çå"
                Meirei  -> "Error"
    in
        gokan ++ gobi

keiyousiMizenn  = keiyousiKatuyou Mizen
keiyousiRenyou1 = keiyousiKatuyou Renyou1
keiyousiRenyou2 = keiyousiKatuyou Renyou2
keiyousiSyuusi  = keiyousiKatuyou Syuusi
keiyousiRentai  = keiyousiKatuyou Rentai
keiyousiKatei   = keiyousiKatuyou Katei


type Dousi =
    Dousi String Katuyou DousiSyurui

type Katuyou
    = Godan String
    | Kami String
    | Shimo String
    | Sahen

katuyouToString katuyou =
    case katuyou of
        Godan gyou -> gyou ++ "‰∫îÊÆµ"
        Kami  gyou -> gyou ++ "‰∏ä‰∏Ä"
        Shimo gyou -> gyou ++ "‰∏ã‰∏Ä"
        Sahen      -> "„ÅïÂ§âÊ†º"

katuyouFromString gyou katuyou =
    case katuyou of
        "‰∫îÊÆµ"   -> Godan gyou |> Just
        "‰∏ä‰∏Ä"   -> Kami  gyou |> Just
        "‰∏ã‰∏Ä"   -> Shimo gyou |> Just
        "„ÅïÂ§âÊ†º" -> Sahen      |> Just
        _ -> Nothing

type DousiSyurui
    = Jidousi
    | Tadousi
    | Ryouhou

dousiSyuruiToString syurui =
    case syurui of
        Jidousi -> "Ëá™ÂãïË©û"
        Tadousi -> "‰ªñÂãïË©û"
        Ryouhou -> "‰∏°Êñπ"

dousiSyuruiFromString syurui =
    case syurui of
        "Ëá™ÂãïË©û" -> Jidousi |> Just
        "‰ªñÂãïË©û" -> Tadousi |> Just
        "‰∏°Êñπ"   -> Ryouhou |> Just
        _ -> Nothing

jidousi : TangoData -> Generator Dousi
jidousi tango =
    tango.dousi
        |> List.filter (\(Dousi _ _ syurui) ->
            syurui == Jidousi || syurui == Ryouhou)
        |> generatorFromList dummyDousi

tadousi : TangoData -> Generator Dousi
tadousi tango =
    tango.dousi
        |> List.filter (\(Dousi _ _ syurui) ->
            syurui == Tadousi || syurui == Ryouhou)
        |> generatorFromList dummyDousi

type alias DousiKu = ( String, Dousi )

dousiKu : TangoData -> Generator DousiKu
dousiKu tango =
    let
        hukusi =
            generatorFromList ""
            [ "„Å®„Å¶„ÇÇ"
            , "ÈùûÂ∏∏„Å´"
            , "„Åø„Çã„Åã„Çâ„Å´"
            , "Â§öÂ∞ë„ÅØ"
            ]

        youni =
            generatorFromList ""
            [ "„Çà„ÅÜ„Å´" ]

        jidou =
             jidousi tango |> map (\d -> ("", d))

        tadou =
            map2
            (\m d -> (m++"„Çí", d))
            (lazy (\_ -> meisiKu tango))
            (tadousi tango)
    in
        choice
        [ jidou
        , jidou -- weight
        , jidou -- weight
        , tadou
        , tadou -- weight
        , tadou -- weight
        , map3 -- ÂãïË©û„ÅÆÈÄ£‰ΩìÂΩ¢Ôºã„Äå„Çà„ÅÜ„Å´„Äç
            (\dk y d2 -> ( dousiKuRentai dk ++ y, d2 ))
            (lazy (\_ -> dousiKu tango))
            youni
            (jidousi tango)
        , map2 -- ÂΩ¢ÂÆπË©û„ÅÆÈÄ£Áî®ÂΩ¢
            (\k ( s, d ) -> ( keiyousiRenyou2 k ++ s, d ))
            (lazy (\_ -> keiyousiGokan tango))
            (lazy (\_ -> dousiKu tango))
        , map2 -- ÂâØË©û
            (\h ( s, d ) -> ( h++s, d ))
            hukusi
            (lazy (\_ -> dousiKu tango))
        ]

type Katuyoukei
    = Mizen
    | Renyou1
    | Renyou2
    | Syuusi
    | Rentai
    | Katei
    | Meirei

dousiKatuyou : Katuyoukei -> Dousi -> String
dousiKatuyou katuyoukei dousi =
    let
        (Dousi gokan katuyou _) =
            dousi
        (Katuyougobi mizen ( renyou1, renyou2 ) syuusi rentai katei meirei) =
            katuyougobi katuyou

        gobi =
            case katuyoukei of
                Mizen -> mizen
                Renyou1 -> renyou1
                Renyou2 -> renyou2
                Syuusi -> syuusi
                Rentai -> rentai
                Katei -> katei
                Meirei -> meirei
    in
        gokan ++ gobi

dousiMizen   = dousiKatuyou Mizen
dousiRenyou1 = dousiKatuyou Renyou1
dousiRenyou2 = dousiKatuyou Renyou2
dousiSyuusi  = dousiKatuyou Syuusi
dousiRentai  = dousiKatuyou Rentai
dousiKatei   = dousiKatuyou Katei
dousiMeirei  = dousiKatuyou Meirei

dousiKuKatuyou : Katuyoukei -> ( String, Dousi ) -> String
dousiKuKatuyou katuyoukei ( syuusyoku, dousi ) =
    syuusyoku ++ dousiKatuyou katuyoukei dousi

dousiKuMizen   = dousiKuKatuyou Mizen
dousiKuRenyou1 = dousiKuKatuyou Renyou1
dousiKuRenyou2 = dousiKuKatuyou Renyou2
dousiKuSyuusi  = dousiKuKatuyou Syuusi
dousiKuRentai  = dousiKuKatuyou Rentai
dousiKuKatei   = dousiKuKatuyou Katei
dousiKuMeirei  = dousiKuKatuyou Meirei

type Katuyougobi =
    Katuyougobi String ( String, String ) String String String  String

katuyougobi : Katuyou -> Katuyougobi
katuyougobi katuyou =
    case katuyou of
        Godan "„Åã" -> Katuyougobi "„Åã" ( "„Åç", "„ÅÑ" ) "„Åè" "„Åè" "„Åë" "„Åë"
        Godan "„Åå" -> Katuyougobi "„Åå" ( "„Åé", "„ÅÑ" ) "„Åê" "„Åê" "„Åí" "„Åí"
        Godan "„Åï" -> Katuyougobi "„Åï" ( "„Åó", "„Åó" ) "„Åô" "„Åô" "„Åõ" "„Åõ"
        Godan "„Åü" -> Katuyougobi "„Åü" ( "„Å°", "„Å£" ) "„Å§" "„Å§" "„Å¶" "„Å¶"
        Godan "„Å™" -> Katuyougobi "„Å™" ( "„Å´", "„Çì" ) "„Å¨" "„Å¨" "„Å≠" "„Å≠"
        Godan "„Å∞" -> Katuyougobi "„Å∞" ( "„Å≥", "„Çì" ) "„Å∂" "„Å∂" "„Åπ" "„Åπ"
        Godan "„Åæ" -> Katuyougobi "„Åæ" ( "„Åø", "„Çì" ) "„ÇÄ" "„ÇÄ" "„ÇÅ" "„ÇÅ"
        Godan "„Çâ" -> Katuyougobi "„Çâ" ( "„Çä", "„Å£" ) "„Çã" "„Çã" "„Çå" "„Çå"
        Godan "„Çè" -> Katuyougobi "„Çè" ( "„ÅÑ", "„Å£" ) "„ÅÜ" "„ÅÜ" "„Åà" "„Åà"
        Kami  ""   -> Katuyougobi "" ( "", "" ) "„Çã" "„Çã" "„Çå" "„Çç"
        Kami  "„ÅÇ" -> Katuyougobi "„ÅÑ" ( "„ÅÑ", "„ÅÑ" ) "„ÅÑ„Çã" "„ÅÑ„Çã" "„ÅÑ„Çå" "„ÅÑ„Çç"
        Kami  "„Åã" -> Katuyougobi "„Åç" ( "„Åç", "„Åç" ) "„Åç„Çã" "„Åç„Çã" "„Åç„Çå" "„Åç„Çç"
        Kami  "„Åå" -> Katuyougobi "„Åé" ( "„Åé", "„Åé" ) "„Åé„Çã" "„Åé„Çã" "„Åé„Çå" "„Åé„Çç"
        Kami  "„Åñ" -> Katuyougobi "„Åò" ( "„Åò", "„Åò" ) "„Åò„Çã" "„Åò„Çã" "„Åò„Çå" "„Åò„Çç"
        Kami  "„Åü" -> Katuyougobi "„Å°" ( "„Å°", "„Å°" ) "„Å°„Çã" "„Å°„Çã" "„Å°„Çå" "„Å°„Çç"
        Kami  "„Å∞" -> Katuyougobi "„Å≥" ( "„Å≥", "„Å≥" ) "„Å≥„Çã" "„Å≥„Çã" "„Å≥„Çå" "„Å≥„Çç"
        Kami  "„Åæ" -> Katuyougobi "„Åø" ( "„Åø", "„Åø" ) "„Åø„Çã" "„Åø„Çã" "„Åø„Çå" "„Åø„Çç"
        Kami  "„Çâ" -> Katuyougobi "„Çä" ( "„Çä", "„Çä" ) "„Çä„Çã" "„Çä„Çã" "„Çä„Çå" "„Çä„Çç"
        Shimo ""   -> Katuyougobi "" ( "", "" ) "„Çã" "„Çã" "„Çå" "„Çç"
        Shimo "„ÅÇ" -> Katuyougobi "„Åà" ( "„Åà", "„Åà" ) "„Åà„Çã" "„Åà„Çã" "„Åà„Çå" "„Åà„Çç"
        Shimo "„Åã" -> Katuyougobi "„Åë" ( "„Åë", "„Åë" ) "„Åë„Çã" "„Åë„Çã" "„Åë„Çå" "„Åë„Çç"
        Shimo "„Åå" -> Katuyougobi "„Åí" ( "„Åí", "„Åí" ) "„Åí„Çã" "„Åí„Çã" "„Åí„Çå" "„Åí„Çç"
        Shimo "„Åï" -> Katuyougobi "„Åõ" ( "„Åõ", "„Åõ" ) "„Åõ„Çã" "„Åõ„Çã" "„Åõ„Çå" "„Åõ„Çç"
        Shimo "„Åñ" -> Katuyougobi "„Åú" ( "„Åú", "„Åú" ) "„Åú„Çã" "„Åú„Çã" "„Åú„Çå" "„Åú„Çç"
        Shimo "„Åü" -> Katuyougobi "„Å¶" ( "„Å¶", "„Å¶" ) "„Å¶„Çã" "„Å¶„Çã" "„Å¶„Çå" "„Å¶„Çç"
        Shimo "„Å†" -> Katuyougobi "„Åß" ( "„Åß", "„Åß" ) "„Åß„Çã" "„Åß„Çã" "„Åß„Çå" "„Åß„Çç"
        Shimo "„Å∞" -> Katuyougobi "„Åπ" ( "„Åπ", "„Åπ" ) "„Åπ„Çã" "„Åπ„Çã" "„Åπ„Çå" "„Åπ„Çç"
        Shimo "„Åæ" -> Katuyougobi "„ÇÅ" ( "„ÇÅ", "„ÇÅ" ) "„ÇÅ„Çã" "„ÇÅ„Çã" "„ÇÅ„Çå" "„ÇÅ„Çç"
        Shimo "„Çâ" -> Katuyougobi "„Çå" ( "„Çå", "„Çå" ) "„Çå„Çã" "„Çå„Çã" "„Çå„Çå" "„Çå„Çç"
        Sahen      -> Katuyougobi "„Åó" ( "„Åó", "„Åó" ) "„Åô„Çã" "„Åô„Çã" "„Åô„Çå" "„Åõ„Çà"
        _          -> Katuyougobi "Error" ( "Error", "Error" ) "Error" "Error" "Error" "Error"

generatorFromList : a -> List a -> Generator a
generatorFromList dummy list =
    case list of
        [] -> Random.constant dummy
        hd :: tl -> Random.uniform hd tl

dummyDousi =
    Dousi "‚óã‚óã" Sahen Ryouhou

dummyKeiyousi =
    "‚óã‚óã„Åó"



-- SERIALIZE


tangoToQuery : TangoData -> Maybe String
tangoToQuery tango =
    tango
        |> tangoToBytes
        |> Base64.fromBytes
        |> Maybe.map base64ToUri

tangoFromQuery : String -> Maybe TangoData
tangoFromQuery query =
    query
        |> uriToBase64
        |> Base64.toBytes
        |> Maybe.andThen tangoFromBytes

tangoToBytes : TangoData -> Bytes
tangoToBytes data =
    let
        encoder =
            BE.sequence
                [ list255Encoder stringEncoder data.meisi
                , list255Encoder stringEncoder data.keiyousi
                , list255Encoder dousiEncoder data.dousi
                ]
    in
        BE.encode encoder

tangoFromBytes : Bytes -> Maybe TangoData
tangoFromBytes bytes =
    let
        decoder =
            list255Decoder stringDecoder |> BD.andThen (\m ->
            list255Decoder stringDecoder |> BD.andThen (\k ->
            list255Decoder dousiDecoder |> BD.map (\d ->
                { meisi = m, keiyousi = k, dousi = d }
            )))
    in
        BD.decode decoder bytes

stringEncoder : String -> BE.Encoder
stringEncoder str =
    BE.sequence
    [ BE.unsignedInt8 <| BE.getStringWidth str
    , BE.string str
    ]

stringDecoder : BD.Decoder String
stringDecoder =
    BD.unsignedInt8
        |> BD.andThen BD.string

dousiEncoder : Dousi -> BE.Encoder
dousiEncoder dousi =
    let
        (Dousi gokan _ _) =
            dousi
    in
        BE.sequence
        [ BE.unsignedInt8 <| BE.getStringWidth gokan
        , BE.string gokan
        , gokanIgaiEncoder dousi
        ]

dousiDecoder : BD.Decoder Dousi
dousiDecoder =
    BD.unsignedInt8
        |> BD.andThen BD.string
        |> BD.andThen gokanIgaiDecoder

gokanIgaiEncoder (Dousi _ katuyoukei syurui) =
    let
        gyou =
            (case katuyoukei of
                Godan g -> gyouToInt g
                Kami  g -> gyouToInt g
                Shimo g -> gyouToInt g
                Sahen   -> Just 0)
            |> Maybe.withDefault 0

        katuyoukeiSyurui =
            katuyoukeiSyuruiToInt katuyoukei

        syuruiInt =
            dousiSyuruiToInt syurui
    in
        syuruiInt * 64 + katuyoukeiSyurui * 16 + gyou
            |> BE.unsignedInt8

gokanIgaiDecoder : String -> BD.Decoder Dousi
gokanIgaiDecoder gokan =
    let
        gyou i =
            gyouFromInt (modBy 16 i)

        katuyoukeiSyurui i =
            katuyoukeiSyuruiFromInt (modBy 64 i // 16)

        dousiSyurui i =
            dousiSyuruiFromInt (i // 64)

        gokanIgai =
            (\i ->
                Maybe.map3
                (\g k s -> Dousi gokan (k g) s)
                (gyou i)
                (katuyoukeiSyurui i)
                (dousiSyurui i)
            )
    in
        BD.unsignedInt8
            |> BD.andThen (
                gokanIgai
                    >> Maybe.map BD.succeed
                    >> Maybe.withDefault BD.fail
            )

list255Encoder : (a -> BE.Encoder) -> List a -> BE.Encoder
list255Encoder aEncoder list =
    BE.sequence <|
        BE.unsignedInt8 (list |> List.length)
            :: (list |> List.reverse |> List.map aEncoder)

list255Decoder : BD.Decoder a -> BD.Decoder (List a)
list255Decoder decoder =
    let
        listStep : BD.Decoder a -> (Int, List a) -> BD.Decoder (BD.Step (Int, List a) (List a))
        listStep decoder_ (n, xs) =
            if n <= 0 then
                BD.succeed (BD.Done xs)
            else
                BD.map (\x -> BD.Loop (n - 1, x :: xs)) decoder_
    in
        BD.unsignedInt8
            |> BD.andThen (\len -> BD.loop (len, []) (listStep decoder))

gyouToInt g =
    case g of
        ""   -> Just 0
        "„ÅÇ" -> Just 1
        "„Åã" -> Just 2
        "„Åå" -> Just 3
        "„Åï" -> Just 4
        "„Åñ" -> Just 5
        "„Åü" -> Just 6
        "„Å†" -> Just 7
        "„Å™" -> Just 8
        "„ÅØ" -> Just 9
        "„Å∞" -> Just 10
        "„Å±" -> Just 11
        "„Åæ" -> Just 12
        "„ÇÑ" -> Just 13
        "„Çâ" -> Just 14
        "„Çè" -> Just 15
        _ -> Nothing

gyouFromInt i =
    case i of
        0  -> Just ""
        1  -> Just "„ÅÇ"
        2  -> Just "„Åã"
        3  -> Just "„Åå"
        4  -> Just "„Åï"
        5  -> Just "„Åñ"
        6  -> Just "„Åü"
        7  -> Just "„Å†"
        8  -> Just "„Å™"
        9  -> Just "„ÅØ"
        10 -> Just "„Å∞"
        11 -> Just "„Å±"
        12 -> Just "„Åæ"
        13 -> Just "„ÇÑ"
        14 -> Just "„Çâ"
        15 -> Just "„Çè"
        _ -> Nothing

katuyoukeiSyuruiToInt katuyoukei =
    case katuyoukei of
        Godan _ -> 0
        Kami _  -> 1
        Shimo _ -> 2
        Sahen   -> 3

katuyoukeiSyuruiFromInt i =
    case i of
        0 -> Just Godan
        1 -> Just Kami
        2 -> Just Shimo
        3 -> Just (\_ -> Sahen)
        _ -> Nothing

dousiSyuruiToInt syurui =
    case syurui of
        Jidousi -> 0
        Tadousi -> 1
        Ryouhou -> 2

dousiSyuruiFromInt i =
    case i of
        0 -> Just Jidousi
        1 -> Just Tadousi
        2 -> Just Ryouhou
        _ -> Nothing

base64ToUri : String -> String
base64ToUri =
    String.replace "+" "*"
        >> String.replace "/" "."
        >> String.replace "=" "-"

uriToBase64 : String -> String
uriToBase64 =
    String.replace "*" "+"
        >> String.replace "." "/"
        >> String.replace "-" "="



-- INIT


init : () -> Url -> Nav.Key -> ( Model, Cmd Msg )
init _ url key =
    ( initModel url key
    , Task.perform UrlChanged <| Task.succeed <| url
    )

initModel : Url -> Nav.Key -> Model
initModel url key =
    { key = key
    , url = url
    , sentences = []
    , tango =
        { meisi = []
        , keiyousi = []
        , dousi = []
        }
    , tuikaSettei =
        { gokan = ""
        , gyou = ""
        , katuyoukei = "‰∫îÊÆµ"
        , syurui = "Ëá™ÂãïË©û"
        }
    }
